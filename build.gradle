apply plugin: 'com.android.application'

repositories {
    google()
    mavenCentral()
}

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.3'
    }
}

android {
    compileSdkVersion 34

    defaultConfig {
        applicationId "nl.igorski.kosm"
        minSdkVersion 23
        targetSdkVersion 34
        versionCode 17
        versionName "2.1.1"

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }

        externalNativeBuild {

            // List of potential arguments: https://developer.android.com/ndk/guides/cmake.html

            cmake {
                arguments "-DANDROID_TOOLCHAIN=clang",
                          "-DANDROID_STL=c++_static"

                cFlags   "-O3"
                cppFlags "-std=c++17 -Werror -Wno-deprecated-declarations -fexceptions -frtti"
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    if ( project.hasProperty( "RELEASE_STORE_FILE" )) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        if ( project.hasProperty( "RELEASE_STORE_FILE" )) {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

clean.doFirst {
    delete fileTree("src/main/java/nl/igorski/mwengine/core") { include '**/*.java' }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar' ])
}
